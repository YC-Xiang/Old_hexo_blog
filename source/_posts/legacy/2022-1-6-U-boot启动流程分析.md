---
layout:     post   				    
title:      U-boot启动流程分析
subtitle:   
date:       2022-1-6	
author:     YC-Xiang 						
header-img:  	
catalog: true 						
tags:								
    - u-boot
---

## uboot三段式启动流程

### Stage 1(Flash)


### Stage 2(SRAM)


### Stage 3(SDRAM)


## armv7架构uboot

- arch级的初始化
    - 关闭中断，设置svc模式
    - 禁用MMU、TLB
    - 关键寄存器的设置，包括时钟、看门狗的寄存器

- 板级的初始化
    - 堆栈环境的设置
    - 代码重定向之前的板级初始化，包括串口、定时器、环境变量、I2C\SPI等等的初始化
    - 进行代码重定向
    - 代码重定向之后的板级初始化，包括板级代码中定义的初始化操作、emmc、nand flash、网络、中断等等的初始化
    - 进入命令行状态，等待终端输入命令以及对命令进行处理

~~~c
	.globl	_start
	.globl	save_boot_params_ret
	.type   save_boot_params_ret,%function

_start:
	/* Allow the board to save important registers */
	b	save_boot_params
save_boot_params_ret:
	/*
	 * disable interrupts (FIQ and IRQ), also set the cpu to SVC32 mode,
	 * except if in HYP mode already
	 */
	mrs	r0, cpsr            // 将程序状态寄存器cpsr的内容传送到r0中
	and	r1, r0, #0x1f       // 读出r0寄存器[0:4]位的值保存到r1寄存器
	teq	r1, #0x1a           // 判断r1寄存器与0x1a是否相等，是否处于HYP模式
	bicne r0, r0, #0x1f     // 不等则清除r0[0:4]位
	orrne r0, r0, #0x13     // 设置成SVC模式
	orr	r0, r0, #0xc0       // 禁止快中断和外部中断
	msr	cpsr,r0             // 将r0传入cpsr
~~~

```c
/*
 * Setup vector:
 * (OMAP4 spl TEXT_BASE is not 32 byte aligned.
 * Continue to use ROM code vector only in OMAP4 spl)
 */

/* the mask ROM code should have PLL and others stable */
#ifndef CONFIG_SKIP_LOWLEVEL_INIT
#ifdef CONFIG_CPU_V7A
	bl	cpu_init_cp15  // 调用cpu_init_cp15，初始化协处理器CP15,从而禁用MMU和TLB。
#endif
#endif

	ldr	r0, =(CONFIG_SYS_FLASH_BASE + CONFIG_RAMINIT_OFFSET)
	ldr	r1, =(CONFIG_LOAD_BASE)
	ldr	r2, =(CONFIG_SYS_FLASH_BASE + CONFIG_RAMINIT_OFFSET \
			+ CONFIG_RAMINIT_SIZE)
	/*
	 * r0 = source address
	 * r1 = target address
	 * r2 = source end address
	 */
1:
	ldr	r3, [r0], #4  // 将r0地址的数据写入r3, 并将新地址r0+4写入r0
	str	r3, [r1], #4  // 将r3的数据写入以r1为地址的存储器，并将新地址r1+4写入r1
	cmp	r0, r2        // 比较r0，r2的值，判断source addr == source end addr
	bne	1b   // 不等则跳回1

	ldr pc,=(CONFIG_LOAD_BASE) // 跳转到CONFIG_LOAD_BASE地址执行指令

ENTRY(save_boot_params) // ENTRY()实际上是一个global symbol的宏
	b	save_boot_params_ret		@ back to my caller
ENDPROC(save_boot_params)
	.weak	save_boot_params

ENTRY(cpu_init_cp15)
	/*
	 * Invalidate L1 I/D
     * 这里只需要知道是对CP15处理器的部分寄存器清零即可。
     * 将协处理器的c7\c8清零等等，各个寄存器的含义请参考《ARM的CP15协处理器的寄存器》
	 */
	mov	r0, #0			// set up for MCR
	mcr	p15, 0, r0, c8, c7, 0	// invalidate TLBs
	mcr	p15, 0, r0, c7, c5, 0	// invalidate icache
	mcr	p15, 0, r0, c7, c5, 6	// invalidate BP array
#if __LINUX_ARM_ARCH__ >= 7
	dsb sy
	isb sy
#else
	mcr     p15, 0, r0, c7, c10, 4	@ DSB
	mcr     p15, 0, r0, c7, c5, 4	@ ISB
#endif

	/*
	 * disable MMU stuff and caches
	 */
	mrc	p15, 0, r0, c1, c0, 0
	bic	r0, r0, #0x00002000	@ clear bits 13 (--V-)
	bic	r0, r0, #0x00000007	@ clear bits 2:0 (-CAM)
	orr	r0, r0, #0x00000002	@ set bit 1 (--A-) Align
	orr	r0, r0, #0x00000800	@ set bit 11 (Z---) BTB
#if CONFIG_IS_ENABLED(SYS_ICACHE_OFF)
	bic	r0, r0, #0x00001000	@ clear bit 12 (I) I-cache
#else
	orr	r0, r0, #0x00001000	@ set bit 12 (I) I-cache
#endif
	mcr	p15, 0, r0, c1, c0, 0

	mov	r5, lr			@ Store my Caller
	mrc	p15, 0, r1, c0, c0, 0	@ r1 has Read Main ID Register (MIDR)
	mov	r3, r1, lsr #20		@ get variant field
	and	r3, r3, #0xf		@ r3 has CPU variant
	and	r4, r1, #0xf		@ r4 has CPU revision
	mov	r2, r3, lsl #4		@ shift variant field for combined value
	orr	r2, r4, r2		@ r2 has combined CPU variant + revision

	mov	pc, r5			@ back to my caller
ENDPROC(cpu_init_cp15)
```

## 参考
- [https://blog.csdn.net/ooonebook/article/details/53070065](https://blog.csdn.net/ooonebook/article/details/53070065)
- [https://blog.csdn.net/ooonebook/article/details/52779230](https://blog.csdn.net/ooonebook/article/details/52779230)
- [mcr指令参考](https://blog.csdn.net/liangzc1124/article/details/112413742?spm=1001.2101.3001.6661.1&utm_medium=distribute.pc_relevant_t0.none-task-blog-2%7Edefault%7ECTRLIST%7Edefault-1.pc_relevant_paycolumn_v2&depth_1-utm_source=distribute.pc_relevant_t0.none-task-blog-2%7Edefault%7ECTRLIST%7Edefault-1.pc_relevant_paycolumn_v2&utm_relevant_index=1)